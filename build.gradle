apply plugin: 'maven' // used for deploying artifacts to Maven repositories

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    ext {
        springPlatformVersion = '1.0.3.RELEASE'
        springBootVersion = '1.1.9.RELEASE'
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

allprojects {
    group = 'org.openmhealth'
    version = '1.1.1-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "http://repo.opensourceagility.com/releases"
        }
        flatDir {
            dirs '../libs'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'spring-boot' // used for blessed dependencies and auto-configuration

    ext {
        gradleVersion = '2.1'
        javaVersion = 1.8
        jacksonVersion = '2.4.2' // platform's still on 2.3.4, 2.4 required for Java 8
        mailApiVersion = '1.5.2'
        postgresqlVersion = '9.3-1102-jdbc41'

    }

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    dependencies {
        // Spring IO (http://platform.spring.io/platform) gives us curated and harmonised dependencies, whose versions
        // you can find at http://docs.spring.io/platform/docs/${springPlatformVersion}/reference/htmlsingle/#appendix-dependency-versions
        versionManagement "io.spring.platform:platform-versions:${springPlatformVersion}@properties"

        compile 'org.hibernate:hibernate-validator'
        compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
        compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
        compile "com.sun.mail:mailapi:${mailApiVersion}"
        compile "javax.mail:javax.mail-api:${mailApiVersion}" // required to support InternetAddress fields
        compile 'org.slf4j:jcl-over-slf4j'
        compile 'org.slf4j:log4j-over-slf4j'
        compile 'ch.qos.logback:logback-classic'
        compile 'org.slf4j:slf4j-api'
        compile 'org.springframework:spring-beans'
        compile "org.springframework.boot:spring-boot-autoconfigure"
        compile 'org.springframework:spring-context'
        compile 'javax.validation:validation-api'
        testCompile 'org.hamcrest:hamcrest-library'
        testCompile 'junit:junit'
        testCompile 'org.mockito:mockito-core'
        testCompile 'org.springframework:spring-test'
    }
}

configure([
        project(":authorization-server"),
        project(":resource-server")]) {

    dependencies {
        compile project(':shared')
        compile 'org.apache.httpcomponents:httpclient'
        compile "org.springframework.boot:spring-boot-starter-jetty"
        compile("org.springframework.boot:spring-boot-starter-web") {
            exclude module: "spring-boot-starter-tomcat"
        }

        // for oauth
        compile 'org.springframework.security.oauth:spring-security-oauth2:2.0.6+'
        compile 'org.springframework:spring-web'
        compile 'org.springframework:spring-webmvc'
        // for template
        compile("org.springframework.boot:spring-boot-starter-thymeleaf")
        // for social sign-in
        compile 'org.springframework.social:spring-social-core'
        compile 'org.springframework.social:spring-social-web'
        compile 'org.springframework.social:spring-social-security'
        // for google social sign-in
        compile 'org.springframework.social:spring-social-google:1.0.0.RELEASE'
        // for determine mobile device requests
        compile 'org.springframework.mobile:spring-mobile-device:1.1.3.RELEASE'

        // you can comment out any dependencies you know you don't need after this point
        compile 'org.mongodb:mongo-java-driver'
        // compile 'mysql:mysql-connector-java'
        compile "org.postgresql:postgresql:${postgresqlVersion}"
    }
}

// FIXME add integration test support

task wrapper(type: Wrapper) {
    gradleVersion = gradleVersion
}
